apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

Properties projectProperties = new Properties()
projectProperties.load(rootProject.file('gradle.properties').newDataInputStream())

Properties localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 35

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 35
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lint {
        abortOnError false
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
    namespace 'com.xlythe.view.clock'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = group = projectProperties.getProperty("bintray.groupId")
                artifactId = projectProperties.getProperty("bintray.artifact")
                version = projectProperties.getProperty("bintray.libraryVersion")

                pom {
                    name = projectProperties.getProperty("bintray.libraryName")
                    description = projectProperties.getProperty("bintray.libraryDescription")
                    url = projectProperties.getProperty("bintray.siteUrl")
                    licenses {
                        license {
                            name = projectProperties.getProperty("bintray.licenseName")
                            url = projectProperties.getProperty("bintray.licenseUrl")
                        }
                    }
                    developers {
                        developer {
                            id = projectProperties.getProperty("bintray.developerId")
                            name = projectProperties.getProperty("bintray.developerName")
                            email = projectProperties.getProperty("bintray.developerEmail")
                        }
                    }
                    scm {
                        connection = projectProperties.getProperty("bintray.gitUrl")
                        developerConnection = projectProperties.getProperty("bintray.gitUrl")
                        url = projectProperties.getProperty("bintray.siteUrl")
                    }
                }
            }
        }
        repositories {
            maven {
                name = "MavenCentral"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = localProperties.getProperty("bintray.username")
                    password = localProperties.getProperty("bintray.password")
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            localProperties.getProperty("signing.keyId"),
            localProperties.getProperty("signing.key"),
            localProperties.getProperty("signing.password"),
    )
    sign publishing.publications
}

dependencies {
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
    def withoutSupportLib = { exclude group: 'com.android.support' }
    api fileTree(include: ['*.jar'], dir: 'libs')
    api 'androidx.wear.watchface:watchface:[1.2.1,)'
    api 'androidx.wear.watchface:watchface-editor:[1.2.1,)'
    api 'androidx.wear.watchface:watchface-complications-rendering:[1.2.1,)'
    api 'androidx.legacy:legacy-support-v4:[1.0.0,)'
    api 'androidx.annotation:annotation:[1.9.1,)'
    api 'androidx.appcompat:appcompat:[1.7.0,)'
    api 'com.google.android.gms:play-services-wearable:[18.2.0,)', withoutSupportLib
}